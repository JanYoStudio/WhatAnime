apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'vip.mystery0.autoversion'
apply plugin: 'kotlin-kapt'

autoVersion {
    major 1
    minor 5
    patch 8
    beta 0
    alpha 0
    isShowBuild beta != 0 || alpha != 0
}

def propertiesFile = new File("local.properties")
Properties properties = new Properties()
if (propertiesFile != null && propertiesFile.exists()) {
    properties.load(new FileInputStream(new File("local.properties")))
}
def signKeyStoreFile = System.getenv("SIGN_KEY_STORE_FILE")
signKeyStoreFile = signKeyStoreFile == null ? properties.get("SIGN_KEY_STORE_FILE") : signKeyStoreFile
def signKeyStorePassword = System.getenv("SIGN_KEY_STORE_PASSWORD")
signKeyStorePassword = signKeyStorePassword == null ? properties.get("SIGN_KEY_STORE_PASSWORD") : signKeyStorePassword
def signKeyAlias = System.getenv("SIGN_KEY_ALIAS")
signKeyAlias = signKeyAlias == null ? properties.get("SIGN_KEY_ALIAS") : signKeyAlias
def signKeyPassword = System.getenv("SIGN_KEY_PASSWORD")
signKeyPassword = signKeyPassword == null ? properties.get("SIGN_KEY_PASSWORD") : signKeyPassword

android {
    packagingOptions {
        exclude 'META-INF/library-core_release.kotlin_module'
    }
    compileSdkVersion 30
    buildFeatures {
        dataBinding = true
    }
    defaultConfig {
        applicationId "pw.janyo.whatanime"
        minSdkVersion 21
        targetSdkVersion 30
        versionCode autoVersion.code
        versionName autoVersion.name
        vectorDrawables.useSupportLibrary = true
        setProperty("archivesBaseName", "WhatAnime-$versionName")
        //指定room.schemaLocation生成的文件路径
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }
    signingConfigs {
        release {
            storeFile file(signKeyStoreFile)
            storePassword signKeyStorePassword
            keyAlias signKeyAlias
            keyPassword signKeyPassword
        }
    }
    buildTypes {
        debug {
            resValue "string", "app_version_name", autoVersion.name
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        release {
            resValue "string", "app_version_name", autoVersion.name
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    lintOptions {
        //设置编译的lint开关，程序在buid的时候，会执行lint检查，有任何的错误或者警告提示，都会终止构建
        abortOnError false
    }
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

def koin_version = '2.1.5'

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    //androidx
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.vectordrawable:vectordrawable:1.1.0'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.preference:preference-ktx:1.1.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation "androidx.browser:browser:1.3.0"
    implementation 'com.google.android.material:material:1.3.0'
    // admod
    implementation 'com.google.android.gms:play-services-ads:19.7.0'
    // Koin AndroidX Scope features
    implementation "org.koin:koin-androidx-scope:$koin_version"
    // Koin AndroidX ViewModel features
    implementation "org.koin:koin-androidx-viewmodel:$koin_version"
    // Koin AndroidX Experimental features
    implementation "org.koin:koin-androidx-ext:$koin_version"
    //Mystery0Tools
    implementation 'vip.mystery0.tools:logs:1.4.5'
    implementation 'vip.mystery0.tools:tools:2.2.9'
    implementation 'vip.mystery0.tools:crashhandler:1.1.4'
    //Condom
    implementation 'com.oasisfeng.condom:library:2.5.0'
    //MMKV
    implementation 'com.tencent:mmkv-static:1.2.1'
    //Retrofit
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:4.9.0'
    //coil
    implementation 'io.coil-kt:coil:1.1.1'
    implementation 'io.coil-kt:coil-gif:1.1.1'
    //Lifecycle
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.2.0'
    kapt 'androidx.lifecycle:lifecycle-common-java8:2.2.0'
    //Room
    implementation 'androidx.room:room-runtime:2.2.6'
    implementation 'androidx.room:room-ktx:2.2.6'
    kapt 'androidx.room:room-compiler:2.2.6'
    //ZLoading
    implementation 'com.zyao89:zloading:1.2.0'
    //Luban
    implementation 'top.zibin:Luban:1.1.8'
    //ExoPlayer
    implementation 'com.google.android.exoplayer:exoplayer:2.13.0'
}
